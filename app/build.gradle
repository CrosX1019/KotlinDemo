plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.crosx.kotlin"
        minSdkVersion 28
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        flavorDimensions "kotlinDemo"
    }

    applicationVariants.all {
        variant ->
            if (variant.buildType.name == 'release') {//防止AS无法安装debug包(apk)
                variant.outputs.all { output ->
                    //打包名字
                    outputFileName = "MyDemo${variant.productFlavors[0].name}_${buildType.name}_v${versionName}.apk"
                }
            }
    }

    buildTypes {
        release {
            // 混淆
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            // 混淆
            minifyEnabled false
            // Zipalign优化
            zipAlignEnabled false
            // 移除无用的resource文件
            shrinkResources false
        }
    }

    productFlavors {

        publish {
            manifestPlaceholders = [label: "正式版本", icon: "@mipmap/ic_launcher"]
        }

        develop {
            manifestPlaceholders = [label: "开发版本", icon: "@mipmap/ic_launcher_round"]
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
        }
    }
}

dependencies {
    implementation project(path: ':common')
    implementation project(path: ':module_home')
    implementation project(path: ':module_personal')
}